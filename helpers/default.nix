{ pkgs, ... }: let
  inherit (pkgs) lib;
  colors = import ./colors.nix { inherit lib; };

in rec {
  inherit colors;
  math = import ./math.nix { inherit lib; };
  strings = import ./strings.nix { inherit lib; };

  mkStellaeStamp = ex: el: ''
  Generated by STELLAE: https://github.com/luna-skye/stellae.git
  Exporter: ${ex}
  Element: ${el}'';
  

  getExporters = path: let
    entries = builtins.readDir path;
    helpers = import ../helpers { inherit pkgs; };
  in lib.mapAttrs (name: type:
    let subpath = path + "/${name}";
    in if type == "directory" then
      if builtins.pathExists (subpath + "/default.nix") then
        (import subpath) { inherit helpers; inherit (pkgs) lib; }
      else getExporters subpath
    else null
  ) entries;


  # Get all .nix files in a directory as a nix-store path list
  getSubmodulePaths = dir: builtins.concatLists (lib.mapAttrsToList (path: kind:
    if (kind == "directory") then
      let file = "${builtins.toPath dir}/${path}/default.nix";
      in if (builtins.pathExists file) then [file] else []
    else
      let file = "${builtins.toPath dir}/${path}";
      in if (builtins.pathExists file) then [file] else []
    )
  (builtins.readDir dir));


  # Imports any found submodules within a path
  importSubmodules = dir: builtins.listToAttrs (builtins.map (path: let
    name = builtins.head (lib.splitString "." (builtins.baseNameOf path));
  in {
    inherit name;
    value = { inherit name; } // import path;
  }) (getSubmodulePaths dir));
} // colors
