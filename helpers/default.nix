{ pkgs, ... }: let
  inherit (pkgs) lib;
  colors = import ./colors.nix { inherit lib; };

in rec {
  inherit colors;
  math = import ./math.nix { inherit lib; };
  strings = import ./strings.nix { inherit lib; };

  mkStellaeStamp = ex: el: ''
  Generated by STELLAE: https://github.com/luna-skye/stellae.git
  Exporter: ${ex}
  Element: ${el}'';
  

  # Import all exporters as a nested attrset from default.nix files
  getExporters = path: let
    entries = builtins.readDir path;
    helpers = import ../helpers { inherit pkgs; };
  in lib.mapAttrs (name: type:
    let subpath = path + "/${name}";
    in if type == "directory" then
      if builtins.pathExists (subpath + "/default.nix") then
        (import subpath) { inherit helpers; inherit (pkgs) lib; }
      else getExporters subpath
    else null
  ) entries;


  # Builds all exporters from a single element
  buildExporters = exporters: element: lib.mapAttrs (name: exporter: (let
    type = builtins.typeOf exporter;
  in
    if type == "lambda" then exporter { inherit element; }
    else if type == "set" then buildExporters exporter element else {}
  )) exporters;


  # Get all .nix files in a directory as a nix-store path list
  getSubmodulePaths = dir: builtins.concatLists (lib.mapAttrsToList (path: kind:
    if (kind == "directory") then
      let file = "${builtins.toPath dir}/${path}/default.nix";
      in if (builtins.pathExists file) then [file] else []
    else
      let file = "${builtins.toPath dir}/${path}";
      in if (builtins.pathExists file) then [file] else []
    )
  (builtins.readDir dir));


  # Imports any found submodules within a path
  getElements = dir: builtins.listToAttrs (builtins.map (path: let
    name = builtins.head (lib.splitString "." (builtins.baseNameOf path));
  in {
    inherit name;
    value =
        # include element name
        { inherit name; } //
        # deep merge with hydrogen element as a base
        lib.recursiveUpdate (import ../elements/hydrogen.nix) (import path);
  }) (getSubmodulePaths dir));
} // colors
